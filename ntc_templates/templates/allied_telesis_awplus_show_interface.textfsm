Value Required INTERFACE (\S+)
Value LINK_STATUS (\w+)
Value PROTOCOL_STATUS (\w+)
Value HARDWARE_TYPE (\w+)
Value MAC_ADDRESS (\S+)
Value DESCRIPTION (\S.*)
Value MTU (\d+)
Value DUPLEX (([Ff]ull|[Aa]uto|[Hh]alf|[Aa]-).*?)
Value SPEED (\w+)

Start
  ^Interface\s\S+ -> Continue.Record
  ^Interface\s${INTERFACE}
  ^\s+Scope:
  ^\s+Thrash-limiting
  ^\s+Status\s+Not\s+Detected
  ^\s+Link is ${LINK_STATUS},\sadministrative state is ${PROTOCOL_STATUS}
  ^\s+Hardware is ${HARDWARE_TYPE}, address is ${MAC_ADDRESS}
  ^\s+Hardware is ${HARDWARE_TYPE}
  ^\s+Description:\s${DESCRIPTION}
  ^\s+index\s+\d+\s+metric\s+\d+\s+m[tr]u\s+${MTU}
  ^\s+arp\s+ageing\s+timeout
  ^\s+Bandwidth
  ^\s+input\s+packets
  ^\s+output\s+packets
  ^\s+current\s+duplex\s+
  ^\s+configured\s+duplex\s+${DUPLEX},\s+configured\s+speed\s+${SPEED},\s+
  ^\s+<.*>
  ^\s+SNMP\s+link-status\s+traps
  ^\s+Time\s+since\s+last\s+state\s+change
  ^. -> Error
